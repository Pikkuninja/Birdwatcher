apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "fi.jara.birdwatcher"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "fi.jara.birdwatcher.TestBirdwatcherApplicationTestRunner"

        buildConfigField "String", "SOURCE_CODE_REPOSITORY_URL", "\"https://github.com/Pikkuninja/birdwatcher\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        animationsDisabled true
    }
}

repositories {
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.kotlinStdLib
    implementation deps.kotlinCoroutines
    implementation deps.kotlinCoroutinesAndroid

    implementation deps.appCompat
    implementation deps.androidKtx
    implementation deps.fragment

    implementation deps.lifecycleKtx
    implementation deps.liveDataKtx
    implementation deps.viewmodelKtx
    implementation deps.navigationKtx
    implementation deps.navigationUIKtx
    implementation deps.room
    implementation deps.roomKtx
    kapt deps.roomKapt

    implementation deps.dagger
    kapt deps.daggerKapt

    implementation deps.picasso
    implementation deps.materialDesign
    implementation deps.constraintLayout
    implementation deps.recyclerView
    implementation deps.cardView

    implementation deps.ossLicenses

    debugImplementation deps.fragmentTesting

    testImplementation deps.junit
    testImplementation deps.mockk
    testImplementation deps.kotlinCoroutinesTesting
    testImplementation deps.lifecycleTesting
    testImplementation deps.liveDataTesting
    testImplementation deps.fragmentTesting

    androidTestImplementation deps.androidTestRunner
    androidTestImplementation deps.androidKtxTesting
    androidTestImplementation (deps.mockkAndroid) { exclude module: 'objenesis' }
    androidTestImplementation 'org.objenesis:objenesis:2.6'
    androidTestImplementation deps.dexopener
    androidTestImplementation deps.espresso
    androidTestImplementation deps.kotlinCoroutinesTesting
    androidTestImplementation deps.lifecycleTesting
    androidTestImplementation deps.liveDataTesting
    androidTestImplementation deps.navigationTesting
    androidTestImplementation deps.roomTesting
}
