apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: "androidx.navigation.safeargs.kotlin"

allOpen {
    annotation 'fi.jara.birdwatcher.common.testing.OpenForTesting'
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "fi.jara.birdwatcher"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "fi.jara.birdwatcher.TestBirdwatcherApplicationTestRunner"

        buildConfigField "String", "SOURCE_CODE_REPOSITORY_URL", "\"https://github.com/Pikkuninja/birdwatcher\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.kotlinStdLib
    implementation deps.kotlinCoroutines
    implementation deps.kotlinCoroutinesAndroid

    implementation deps.appCompat
    implementation deps.androidKtx
    implementation deps.fragment

    implementation deps.lifecycleKtx
    implementation deps.liveDataKtx
    implementation deps.viewmodelKtx
    implementation deps.navigationKtx
    implementation deps.navigationUIKtx
    implementation deps.room
    implementation deps.roomKtx
    kapt deps.roomKapt

    implementation deps.dagger
    kapt deps.daggerKapt

    implementation deps.picasso
    implementation deps.materialDesign
    implementation deps.constraintLayout
    implementation deps.recyclerView
    implementation deps.cardView

    implementation deps.ossLicenses

    testImplementation deps.junit
    testImplementation deps.mockk
    testImplementation deps.kotlinCoroutinesTesting
    testImplementation deps.lifecycleTesting
    testImplementation deps.liveDataTesting
    testImplementation deps.fragmentTesting
    kaptTest deps.daggerKapt
    androidTestImplementation deps.androidTestRunner
    androidTestImplementation deps.androidKtxTesting
    androidTestImplementation deps.espresso
    androidTestImplementation deps.lifecycleTesting
    androidTestImplementation deps.liveDataTesting
    androidTestImplementation deps.roomTesting
    kaptAndroidTest deps.daggerKapt
    debugImplementation deps.fragmentTesting
}
